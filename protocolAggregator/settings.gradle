import java.nio.file.Files
import java.nio.file.Paths

pluginManagement {
    repositories {
        maven {
            def path = Paths.get(rootDir.absolutePath).resolve(conclaveRepo).toAbsolutePath().normalize()
            if (!Files.isDirectory(path.resolve("com"))) {
                if (Files.isDirectory(Paths.get("/repo/com"))) {
                    path = Paths.get("/repo")
                } else {
                    throw new Exception("Neither $path nor /repo seem to exist, or they aren't Maven repositories; it should be the SDK 'repo' subdirectory. " +
                            "If on macOS, try using the container-gradle script to execute run the tests. See the Conclave CorDapp tutorial on https://docs.conclave.net/writing-cordapps.html")
                }
            }
            url = path.toFile()
        }
        // Add standard repositories back.
        gradlePluginPortal()
        jcenter()
        mavenCentral()
    }

    plugins {
        id 'com.r3.conclave.enclave' version conclaveVersion apply false
    }
}

gradle.ext.moduleProjects=[]
include 'aggregator-commons'
include 'enclave'
include 'sampleEnclave'
includeModule 'workflows'
includeModule 'contracts'

// projects added with this function produce artifacts that are published to Maven Central
def includeModule(name) {
    gradle.ext.moduleProjects.add(name)
    include ":$name"
}